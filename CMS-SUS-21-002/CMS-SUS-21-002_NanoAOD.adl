#info analysis
#  title "Search for electroweak production of supersymmetric particles in final states containing hadronic decays of WW, WZ, or WH and missing transverse momentum"
#  experiment CMS
#  id SUS-21-002
#  publication Phys. Lett. B 842 (2023) 137460
#  sqrtS 13.0
#  lumi 137
#  arXiv 2205.09597
#  hepdata https://www.hepdata.net/record/ins2085373
#  doi https://doi.org/10.1016/j.physletb.2022.137460

#info adl
#  author S. Sekmen

# OBJECT SELECTION

object ak4jets
  take Jet
  select pT > 30
  select abs(Eta) < 2.4

object bjets
  take ak4jets
#  select DeepCSVM > 0.6321, 0.4941, 0.4184
  select DeepCSVM > 0.6321

object ak8jets
  take FatJet
  select pT > 200
  select abs(Eta) < 2

object ak8jetsm50
  take ak8jets
  select m > 50

object ak8jetsmV
  take ak8jets
  select m [] 65 105

object Hcands
  take ak8jets
  select m [] 75 140
  select dR(ak8jets, bjets) < 0.8

object Wcands
   take ak8jets
  select m [] 65 105
  select dR(ak8jets, bjets) > 0.8

object Htags
  take Hcands
  select pfMassDecorrelatedDeepBoostedDiscriminatorsJetTagsbbvsLight == 1

object Hnotags
  take Hcands
  select pfMassDecorrelatedDeepBoostedDiscriminatorsJetTagsbbvsLight == 1

object Wtags
  take Wcands 
  select pfDeepBoostedDiscriminatorsJetTagsWvsQCD == 1

object Wnotags
  take Wcands 
  select pfDeepBostedDiscriminatorsJetTagsWvsQCD == 0

object Ws
  take ak8jetsmV
  select pfDeepBoostedDiscriminatorsJetTagsWvsQCD

object Vs
  take ak8jetsmV
  select pfMassDecorrelatedDeepBoostedDiscriminatorsJetTagsWvsQCD

object vetoelectrons
  take Electron
  select pT > 10
  select abs(eta) < 2.5
  select Fall17v2CutBasedvetoID == 1
  select miniiso < 0.1

object vetomuons
  take Muon
  select pT > 10
  select abs(eta) < 2.4
  select mediumID == 1
  select miniiso < 0.2

object vetoleptons : union(vetoelectrons, vetomuons)

object isolatedtracks
  take Track
  select abs(pdgID) == 11 or abs(pdgID) == 13 ? pT > 5 : pT > 10
  #define Itk = sum(pT(other charged tracks within dR = 0.3) / pT # syntax
  #select abs(pdgID) == 11 or abs(pdgID) == 13 ? Itk < 0.2 : Itk > 0.1
  select fMT(Track, METLV[0]) < 100
  select dz(track, PV) - dz(track, vertices) < 0

object vetophotons
  take Photon
  select pT > 100
  select abs(eta) < 1.4442 and abs(eta) [] 1.566 2.5 
  select Fall17v2Loose == 1

# EVENT VARIABLES
define HT = sum(pT(ak4jets))
define MET = pT(METLV[0])

# EVENT SELECTION

region baseline
  #trigger MET[90..140] # syntax
  select size(vetoleptons) == 0
  select size(isolatedtracks) == 0
  select size(vetophotons) == 0
  select MET > 200
  select HT > 300
  select size(ak4jets) [] 2 6
  select size(ak8jets) > 1
  select size(ak8jetsm50) > 1 # syntax: can we do any(m(ak8jets)) > 50 
  select dphi(ak4jets[0], METLV[0]) > 1.5
  select dphi(ak4jets[1], METLV[0]) > 0.5
  select size(ak4jets) > 2 ? dphi(ak4jets[2], METLV[0]) > 0.3 : ALL
  select size(ak4jets) > 3 ? dphi(ak4jets[3], METLV[0]) > 0.3 : ALL
  select dphi(ak8jets[0], METLV[0]) > 1.5
  select dphi(ak8jets[1], METLV[0]) > 0.5  

region bveto
  baseline
  select n(bjets) == 0

region SRbveto
  bveto
  select n(Ws) >= 1
  select n(Vs) >= 1
  select n(Ws) + n(Vs) >= 2
  bin MET 200 250 300 350 400 450 500 600 800 

region CRbveto0tag
  bveto
  select n(bjets) == 0
  select n(Ws) == 0
  select n(Vs) == 0
  bin MET 200 250 300 350 400 450 500 600 800 

region CRbveto1tag
  type control
  bveto
  select n(bjets) == 0
  select n(Vs) == 1
  select n(Ws) == 0 # syntax: other W-tagged jet?
  bin MET 200 250 300 350 400 450 500 600 800 

region btag
  baseline
  select n(bjets) > 0
  
region SRbtagHW
  btag
  select n(Wtags) >= 1
  select n(Htags) >= 1
  bin MET 200 250 300 350 400 450 500 600 

region SRbtagW
  btag 
  select n(Wtags) >= 1
  select n(Htags) == 0
  bin MET 200 250 300 350 400 450 500 600 800 

region SRbtagH
  btag 
  select n(Wtags) == 0
  select n(Htags) >= 1
  bin MET 200 250 300 350 400 450 500 600 800 

region CRbtagHWantitag
  btag
  select n(Wnotags) >= 1
  select n(Hnotags) >= 1
  bin MET 200 250 300 350 400 450 500 600 

region CRbtagWantitag
  btag 
  select n(Wnotags) >= 1
  select n(Hnotags) == 0
  bin MET 200 250 300 350 400 450 500 600 800 

region CRbtagHantitag
  btag 
  select n(Wnotags) == 0
  select n(Hnotags) >= 1
  bin MET 200 250 300 350 400 450 500 600 800 

